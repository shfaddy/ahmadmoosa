instr tak

aLeft, aRight subinstr "_tak"

aLeft clip aLeft, 1, 0dbfs
aRight clip aRight, 1, 0dbfs

chnmix aLeft / p4, "left"
chnmix aRight / p4, "right"

endin

instr _tak

iPitch random 0, 1

iAttack init 1/128
iDecay init 1/64 
iRelease init 1/64

p3 init iAttack + iDecay + iRelease

aLowSubAmplitude linseg 0, iAttack, 1, iDecay, .05, iRelease, 0
aLowSubFrequency linseg cpsoct ( 12 + iPitch ), iAttack/1, cpsoct ( 7 + iPitch )

aLowSub poscil aLowSubAmplitude, aLowSubFrequency

iLowSub init 1

outs aLowSub / iLowSub, aLowSub / iLowSub

aHighSubAmplitude linseg 0, iAttack, 1, iDecay/8, .25, iRelease/8, 0
aHighSubFrequency linseg cpsoct ( 14 + iPitch ), iAttack/1, cpsoct ( 8 + iPitch )

aHighSub poscil aHighSubAmplitude, aHighSubFrequency

iHighSub init 4

outs aHighSub / iHighSub, aHighSub / iHighSub

aGogobell gogobel 1, cpsoct ( 6 + iPitch ), .75, .75, giStrikeFT, 6.0, 0.3, giVibratoFT

iGogobell init 4

outs aGogobell / iGogobell, aGogobell / iGogobell

aSnatchAmplitude linseg 0, iAttack/2, 1, iDecay/8, 0
aSnatchFrequency linseg cpsoct ( 13 + iPitch ), iAttack/2, cpsoct ( 11 + iPitch )

aSnatch noise aSnatchAmplitude, 0
aSnatch butterlp aSnatch, aSnatchFrequency

iSnatch init 1

outs aSnatch / iSnatch, aSnatch / iSnatch

endin
